use crate::ast::*;

grammar;

Tier<Op,NextTier>: Expr = {
    <lhs:Tier<Op,NextTier>> <op:Op> <rhs:NextTier> => Expr::Comp(op, Box::new(lhs), Box::new(rhs)),
	NextTier
};

List<Over>: Vec<Over> = {
	<ls:List<Over>> "," <o:Over> => {
		let mut ls = ls;
		ls.push(o);
		ls
	},
	<o:Over> => vec![o],
};

Set: Vec<(Option<Expr>, Expr)> = {
    <s:Set> "," <v:Expr> => {
        let mut s = s;
        match v {
            Expr::Comp(Operator::Store, key, val) => s.push((Some(*key), *val)),
            _ => s.push((None, v)),
        }
        s
    },
    <Expr> => {
        match <> {
            Expr::Comp(Operator::Store, key, val) => vec![(Some(*key), *val)],
            _ => vec![(None, <>)],
        }
    },
};

Log: LogType = {
	"true" => true,
	"false" => false,
};
Num: NumType = {
    r"[+-]?\d+(\.\d+)?" => numeric(<>),
};
Str: lovm::Str = r"'.*'" => <>.parse::<lovm::Str>().unwrap();

Nil: Value = {
	"Nil" => Value::Nil
};

Tuple = List<Expr>;
Ident: String = r"[a-zA-z][\w']*\??" => String::from(<>);

Value: Expr = {
    Log => Expr::Value(Value::Logical(<>)),
    Num => Expr::Value(Value::Numeric(<>)),
    Str => Expr::Value(Value::Str(<>)),
    Nil => Expr::Value(<>),
    Ident => Expr::Ref(<>),
	"(" <t:Tuple> ")" => {
		let mut t = t;
		if t.len() == 1 {
			t.remove(0)
		} else {
			Expr::Value(Value::Tuple(t))
		}
	},
	"{" <s:Set?> <g:("|" <Set>)?> "}" => match (s, g) {
		(Some(s), Some(g)) => {
			//let gen = GenType::new();
			//Expr::Value(Value::Gen(s, gen))
            unimplemented!()
		}
		(Some(s), _) => Expr::Value(Value::Set(s)),
		_ => Expr::Value(Value::Set(vec![])),
	},
    <i:Ident> "(" <t:Tuple?> ")" => Expr::Func(i, if let Some(t) = t {t} else {vec![]}),
};

SumOps: Operator = {
	"+" => Operator::Add,
	"-" => Operator::Sub,
};

FactorOps: Operator = {
	"*" => Operator::Mul,
	"/" => Operator::Div,
	"%" => Operator::Rem,
	"^" => Operator::Pow,
};

OrOps: Operator = {
	"||" => Operator::Or,
};

AndOps: Operator = {
	"&&" => Operator::And,
};

CompareOps: Operator = {
    "==" => Operator::Eq,
    "!=" => Operator::Ne,
    ">=" => Operator::Ge,
    "<=" => Operator::Le,
    ">" => Operator::Gt,
    "<" => Operator::Lt,
};

Assign: Operator = {
	"=" => Operator::Store,
};

Computations = Tier<SumOps, Tier<FactorOps, Value>>;
Compares = Tier<CompareOps, Computations>;
Logic = Tier<OrOps, Tier<AndOps, Compares>>;
pub Expr = Tier<Assign, Logic>;
